## 标题
可重入性

## 关系
[CWE-841：行为工作流程的不正确执行](https://cwe.mitre.org/data/definitions/347.html)

## 描述
调用外部合约的一个主要危险是它们可以接管控制流程。在重入攻击（也称为递归调用攻击）中，恶意合约在函数的第一次调用完成之前回调到调用它的合约。这可能导致函数的不同调用以不希望的方式相互交互。

## 修复措施
避免重入性弱点的最佳实践包括：

* 确保在执行调用之前执行所有内部状态更改。这被称为[检查-效果-交互模式](https://solidity.readthedocs.io/en/latest/security-considerations.html#use-the-checks-effects-interactions-pattern)
* 使用重入锁（例如[OpenZeppelin的ReentrancyGuard](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/security/ReentrancyGuard.sol）))。
## 参考资料
* [以太坊智能合约最佳实践-重入性](https://consensys.github.io/smart-contract-best-practices/attacks/reentrancy/)
## 示例
### modifier_reentrancy.sol
``` solidity

pragma solidity ^0.5.0;

contract ModifierEntrancy {

  mapping (address => uint) public tokenBalance;
  string constant name = "Nu Token";
  Bank bank;

  constructor() public{
      bank = new Bank();
  }

  //如果一个合约的余额为零并且支持代币，给它一些代币。
  function airDrop() hasNoBalance supportsToken  public{
    tokenBalance[msg.sender] += 20;
  }

  //检查合同是否按我们所希望的方式进行响应。
  modifier supportsToken() {
    require(keccak256(abi.encodePacked("Nu Token")) == bank.supportsToken());
    _;
  }

  //检查调用者是否有零余额
  modifier hasNoBalance {
      require(tokenBalance[msg.sender] == 0);
      _;
  }
}

contract Bank{

    function supportsToken() external returns(bytes32) {
        return keccak256(abi.encodePacked("Nu Token"));
    }

}
```

### modifier_reentrancy_fixed.sol
``` solidity
pragma solidity ^0.5.0;

contract ModifierEntrancy {
  mapping (address => uint) public tokenBalance;
  string constant name = "Nu Token";
  Bank bank;
  constructor() public{
      bank = new Bank();
  }

  //如果一个合约的余额为零并且支持代币，给它一些代币。
  function airDrop() supportsToken hasNoBalance  public{ // 在修复版本中，supportsToken在hasNoBalance之前。
    tokenBalance[msg.sender] += 20;
  }

  //检查合同是否按我们所希望的方式进行响应。
  modifier supportsToken() {
    require(keccak256(abi.encodePacked("Nu Token")) == bank.supportsToken());
    _;
  }
  //检查调用者是否有零余额
  modifier hasNoBalance {
      require(tokenBalance[msg.sender] == 0);
      _;
  }
}

contract Bank{

    function supportsToken() external returns(bytes32){
        return(keccak256(abi.encodePacked("Nu Token")));
    }
}
```

### simple_dao.sol
``` solidity
/*
 * @来源: http://blockchain.unica.it/projects/ethereum-survey/attacks.html#simpledao
 * @作者: Atzei N., Bartoletti M., Cimoli T
 * 由Josselin Feist修改
 */
pragma solidity 0.4.24;

contract SimpleDAO {
  mapping (address => uint) public credit;

  function donate(address to) payable public{
    credit[to] += msg.value;
  }

  function withdraw(uint amount) public{
    if (credit[msg.sender]>= amount) {
      require(msg.sender.call.value(amount)());
      credit[msg.sender]-=amount;
    }
  }  

  function queryCredit(address to) view public returns(uint){
    return credit[to];
  }
}
```

### simple_dao_fixed.sol
``` solidity
/*
 * @来源1: http://blockchain.unica.it/projects/ethereum-survey/attacks.html#simpledao
 * @作者: Atzei N., Bartoletti M., Cimoli T
 * 由Bernhard Mueller, Josselin Feist修改
 */
pragma solidity 0.4.24;

contract SimpleDAO {
  mapping (address => uint) public credit;

  function donate(address to) payable public{
    credit[to] += msg.value;
  }

  function withdraw(uint amount) public {
    if (credit[msg.sender]>= amount) {
      credit[msg.sender]-=amount;
      require(msg.sender.call.value(amount)());
    }
  }  

  function queryCredit(address to) view public returns (uint){
    return credit[to];
  }
}
```
