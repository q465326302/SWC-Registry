**请注意，此内容已不再积极维护。**
自2020年以来，SWC注册表的内容没有得到彻底的更新。已知它不完整，可能包含错误以及关键的遗漏。

当前对于已知的智能合约漏洞的维护指导，主要为安全审查员编写，请参阅[EEA EthTrust安全级别规范](https://entethalliance.org/specs/ethtrust-sl)。除了最新的发布版本之外，还有一个[编辑草案，代表了制定规范的小组的最新工作](https://entethalliance.github.io/eta-registry/security-levels-spec.html)。

现在有关开发人员如何确保安全性的指南，可以通过[智能合约安全验证标准（SCSVS）](https://github.com/ComposableSecurity/SCSVS)，或[智能合约安全领域指南](https://scsfg.io/)获得。

## 标题
函数的默认可见性

## 关系
[CWE-710：缺乏对编码规范的正确遵循](https://cwe.mitre.org/data/definitions/710.html)

## 描述
未指定函数可见性类型的函数默认为public。如果开发人员忘记设置可见性，恶意用户可能会进行未经授权或意外的状态更改，这可能导致漏洞。

## 修复措施
函数可以被指定为external、public、internal或private。建议对函数选择适当的可见性类型做出有意识的决策。这可以大大减少合约系统的攻击面。

## 参考资料
* [以太坊智能合约最佳实践-可见性](https://consensys.github.io/smart-contract-best-practices/development-recommendations/solidity-specific/visibility/)
* [SigmaPrime-可见性](https://github.com/sigp/solidity-security-blog#visibility)

## 示例

### visibility_not_set.sol
``` solidity
/*
 * @来源: https://github.com/sigp/solidity-security-blog#visibility
 * @作者: SigmaPrime 
 * 由Gerhard Wagner修改
 */

pragma solidity ^0.4.24;

contract HashForEther {

    function withdrawWinnings() {
        // 如果地址的最后8个十六进制字符为0，则获胜。
        require(uint32(msg.sender) == 0);
        _sendWinnings();
     }

     function _sendWinnings() {
         msg.sender.transfer(this.balance);
     }
}
```

**评论**
第11行和第17行的函数声明没有设置函数的可见性。至少对于Solidity 0.4.24（如pragma语句中指定的），这意味着它们将默认被视为公共的。这允许任何人调用_sendWinings()函数并取走钱。

相反，下面的修复版本将_sendWinnings()函数的可见性限制为内部，因此只能由WithdrawWinnings()函数激活，该函数强制检查发送者是否真的满足了接收钱的预设条件。

### visibility_not_set_fixed.sol
``` solidity
/*
 * @来源: https://github.com/sigp/solidity-security-blog#visibility
 * @作者: SigmaPrime
 * 由Gerhard Wagner修改
 */

pragma solidity ^0.4.24;

contract HashForEther {

    function withdrawWinnings() public {
        // 如果地址的最后8个十六进制字符为0，则为赢家。
        require(uint32(msg.sender) == 0);
        _sendWinnings();
     }

     function _sendWinnings() internal{
         msg.sender.transfer(this.balance);
     }
}
```

## 验证
未指定函数可见性类型的函数会出现warning警告。
![1-0.png](./img/1-0.png)