## 标题
未初始化的存储指针

## 关系
[CWE-824：访问未初始化指针](https://cwe.mitre.org/data/definitions/824.html)

## 描述
未初始化的本地存储变量可能指向合约中意外的存储位置，这可能导致有意或无意的漏洞。

## 修复措施
检查合约是否需要存储对象，因为在许多情况下实际上并非如此。如果一个本地变量足够，用memory属性显式地标记变量的存储位置。如果需要存储变量，则在声明时初始化它，并额外指定存储位置storage。

注意：从编译器版本0.5.0及更高版本开始，该问题已得到系统解决，因为具有未初始化存储指针的合约不再编译。

## 参考资料
* [SigmaPrime - 未初始化的存储指针](https://github.com/sigp/solidity-security-blog#unintialised-storage-pointers-1)
## 示例
### crypto_roulette.sol
``` solidity

/*
 * @source: https://github.com/thec00n/smart-contract-honeypots/blob/master/CryptoRoulette.sol
 */
pragma solidity ^0.4.19;

// CryptoRoulette
//
// 猜测在区块链中秘密存储的数字并赢得整个合约余额！每次尝试后都会随机选择一个新数字。
//
// 要玩游戏，请调用play()方法并猜测数字（1-20）。赌注价格：0.1以太

contract CryptoRoulette {

    uint256 private secretNumber;
    uint256 public lastPlayed;
    uint256 public betPrice = 0.1 ether;
    address public ownerAddr;

    struct Game {
        address player;
        uint256 number;
    }
    Game[] public gamesPlayed;

    function CryptoRoulette() public {
        ownerAddr = msg.sender;
        shuffle();
    }

    function shuffle() internal {
        // 使用1到20之间的值随机设置secretNumber
        secretNumber = uint8(sha3(now, block.blockhash(block.number-1))) % 20 + 1;
    }

    function play(uint256 number) payable public {
        require(msg.value >= betPrice && number <= 10);

        Game game;
        game.player = msg.sender;
        game.number = number;
        gamesPlayed.push(game);

        if (number == secretNumber) {
            // win!
            msg.sender.transfer(this.balance);
        }

        shuffle();
        lastPlayed = now;
    }

    function kill() public {
        if (msg.sender == ownerAddr && now > lastPlayed + 1 days) {
            suicide(msg.sender);
        }
    }

    function() public payable { }
}
```
### crypto_roulette_fixed.sol
``` solidity
/*
 * @source: https://github.com/thec00n/smart-contract-honeypots/blob/master/CryptoRoulette.sol
 */
pragma solidity ^0.4.19;

// CryptoRoulette
//
// 猜测在区块链中秘密存储的数字并赢得整个合约余额！每次尝试后都会随机选择一个新数字。
//
// 要玩游戏，请调用play()方法并猜测数字（1-20）。赌注价格：0.1以太

contract CryptoRoulette {

    uint256 private secretNumber;
    uint256 public lastPlayed;
    uint256 public betPrice = 0.1 ether;
    address public ownerAddr;

    struct Game {
        address player;
        uint256 number;
    }
    Game[] public gamesPlayed;

    function CryptoRoulette() public {
        ownerAddr = msg.sender;
        shuffle();
    }

    function shuffle() internal {
        // 使用区块链信息和时间戳生成一个1到20之间的随机数
        secretNumber = uint8(sha3(now, block.blockhash(block.number-1))) % 20 + 1;
    }

    function play(uint256 number) payable public {
        require(msg.value >= betPrice && number <= 10);

        Game memory game;
        game.player = msg.sender;
        game.number = number;
        gamesPlayed.push(game);

        if (number == secretNumber) {
            // win!
            msg.sender.transfer(this.balance);
        }

        shuffle();
        lastPlayed = now;
    }

    function kill() public {
        if (msg.sender == ownerAddr && now > lastPlayed + 1 days) {
            suicide(msg.sender);
        }
    }

    function() public payable { }
}
```