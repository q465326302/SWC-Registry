## 标题
意外的以太余额

## 关系
[CWE-667: 不正确的锁定](https://cwe.mitre.org/data/definitions/667.html)

## 描述
当合约严格假设特定的以太余额时，合约可能会出现错误行为。可以通过使用selfdestruct或通过向账户进行挖矿来强制向合约发送以太（而不触发其回退函数）。在最坏的情况下，这可能导致DOS条件，使合约无法使用。

## 修复措施
避免在合约中对以太余额进行严格的相等性检查。

## 参考资料
* [Consensys 最佳实践：强制喂食](https://consensys.github.io/smart-contract-best-practices/attacks/force-feeding/)
* [Sigmaprime：意外的以太](https://blog.sigmaprime.io/solidity-security.html#ether)
* [Gridlock（一个智能合约的错误）](https://medium.com/@nmcl/gridlock-a-smart-contract-bug-73b8310608a9)

## 示例

### Lockdrop.sol
```solidity
/** 
 * @来源: https://github.com/hicommonwealth/edgeware-lockdrop/blob/93ecb524c9c88d25bab36278541f190fa9e910c2/contracts/Lockdrop.sol
 */

pragma solidity ^0.5.0;

contract Lock {
    // 地址所有者；插槽 #0
    // 地址解锁时间; 插槽 #1
    constructor (address owner, uint256 unlockTime) public payable {
        assembly {
            sstore(0x00, owner)
            sstore(0x01, unlockTime)
        }
    }

    /**
     * @dev        当时间戳超过解锁时间后，撤回函数。
     */
    function () external payable { // 可支付的，以确保 Solidity 不会添加不必要的逻辑。
        assembly {
            switch gt(timestamp, sload(0x01))
            case 0 { revert(0, 0) }
            case 1 {
                switch call(Gas, sload(0x00), balance(address), 0, 0, 0, 0)
                case 0 { revert(0, 0) }
            }
        }
    }
}

contract Lockdrop {
    enum Term {
        ThreeMo,
        SixMo,
        TwelveMo
    }
    // 时间常数
    uint256 constant public LOCK_DROP_PERIOD = 1 days * 92; // 3个月
    uint256 public LOCK_START_TIME;
    uint256 public LOCK_END_TIME;
    // ETH锁定事件
    event Locked(address indexed owner, uint256 eth, Lock lockAddr, Term term, bytes edgewareAddr, bool isValidator, uint time);
    event Signaled(address indexed contractAddr, bytes edgewareAddr, uint time);

    constructor(uint startTime) public {
        LOCK_START_TIME = startTime;
        LOCK_END_TIME = startTime + LOCK_DROP_PERIOD;
    }

    /**
     * @dev        在一个新的锁中锁定发送给合约的价值。
     * @param      term         锁定期的长度
     * @param      edgewareAddr 目标 edgeware 密钥的字节表示形式
     * @param      isValidator  表示发送方是否希望成为验证者。
     */
    function lock(Term term, bytes calldata edgewareAddr, bool isValidator)
        external
        payable
        didStart
        didNotEnd
    {
        uint256 eth = msg.value;
        address owner = msg.sender;
        uint256 unlockTime = unlockTimeForTerm(term);
        // 创建ETH锁定合约
        Lock lockAddr = (new Lock).value(eth)(owner, unlockTime);
        // 确保锁定合约中有所有的以太，否则失败。
        assert(address(lockAddr).balance == msg.value);
        emit Locked(owner, eth, lockAddr, term, edgewareAddr, isValidator, now);
    }

    /**
     * @dev        在锁定期之后，表示合约（或地址）的余额决定。
     * @param      contractAddr  用于表示余额的合约地址。
     * @param      nonce         合约创建者的交易 nonce。
     * @param      edgewareAddr   目标 edgeware 密钥的字节表示形式。
     */
    function signal(address contractAddr, uint32 nonce, bytes calldata edgewareAddr)
        external
        didStart
        didNotEnd
        didCreate(contractAddr, msg.sender, nonce)
    {
        emit Signaled(contractAddr, edgewareAddr, now);
    }

    function unlockTimeForTerm(Term term) internal view returns (uint256) {
        if (term == Term.ThreeMo) return now + 92 days;
        if (term == Term.SixMo) return now + 183 days;
        if (term == Term.TwelveMo) return now + 365 days;

        revert();
    }

    /**
     * @dev        确保锁仓已经开始。
     */
    modifier didStart() {
        require(now >= LOCK_START_TIME);
        _;
    }

    /**
     * @dev        确保锁仓尚未结束。
     */
    modifier didNotEnd() {
        require(now <= LOCK_END_TIME);
        _;
    }

    /**
     * @dev        Rebuilds  从普通地址和交易 nonce 重新构建合约地址。
     * @param      _origin  从用户的公钥派生的非合约地址。
     * @param      _nonce   用于生成合约地址的交易 nonce。
     */
    function addressFrom(address _origin, uint32 _nonce) public pure returns (address) {
        if(_nonce == 0x00)     return address(uint160(uint256(keccak256(abi.encodePacked(byte(0xd6), byte(0x94), _origin, byte(0x80))))));
        if(_nonce <= 0x7f)     return address(uint160(uint256(keccak256(abi.encodePacked(byte(0xd6), byte(0x94), _origin, uint8(_nonce))))));
        if(_nonce <= 0xff)     return address(uint160(uint256(keccak256(abi.encodePacked(byte(0xd7), byte(0x94), _origin, byte(0x81), uint8(_nonce))))));
        if(_nonce <= 0xffff)   return address(uint160(uint256(keccak256(abi.encodePacked(byte(0xd8), byte(0x94), _origin, byte(0x82), uint16(_nonce))))));
        if(_nonce <= 0xffffff) return address(uint160(uint256(keccak256(abi.encodePacked(byte(0xd9), byte(0x94), _origin, byte(0x83), uint24(_nonce))))));
        return address(uint160(uint256(keccak256(abi.encodePacked(byte(0xda), byte(0x94), _origin, byte(0x84), uint32(_nonce)))))); // more than 2^32 nonces not realistic
    }

    /**
     * @dev        Ensures 目标地址是由某个父地址在某个 nonce 下创建的。
     * @param      target  目标合约地址（或简单地说，父级地址）。
     * @param      parent  合约地址的创建者。
     * @param      nonce   在合约创建时的创建者的交易 nonce。
     */
    modifier didCreate(address target, address parent, uint32 nonce) {
        // 可以轻松地让发送者“创建”自己。
        if (target == parent) {
            _;
        } else {
            require(target == addressFrom(parent, nonce));
            _;
        }
    }
}
```