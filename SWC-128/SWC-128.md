## 标题
利用区块的燃气限制来发动 DoS 攻击。

## 关系
[CWE-400：未受控制的资源消耗](https://cwe.mitre.org/data/definitions/400.html)

## 描述
当部署智能合约或调用其内部函数时，执行这些操作总是需要一定数量的燃气，根据完成它们所需的计算量而定。以太坊网络规定了区块燃气限制，一个区块中包含的所有交易的总总燃气消耗不能超过这个阈值。

在集中式应用程序中无害的编程模式，当执行函数的成本超过区块燃气限制时，可能导致智能合约的拒绝服务情况。修改一个大小未知且随时间增长的数组，可能导致此类拒绝服务情况。

## 修复措施
当你预计会有随时间增长的大型数组时，请谨慎行事。应避免需要遍历整个数据结构的操作。

如果你确实必须遍历一个未知大小的数组，那么你应该考虑它可能需要多个区块，因此需要多个交易来完成。

## 参考资料
* [以太坊设计理念](https://github.com/ethereum/wiki/wiki/Design-Rationale#gas-and-fees)
* [以太坊黄皮书](https://ethereum.github.io/yellowpaper/paper.pdf)
* [在不超过燃气限制的情况下清除大型数组](https://ethereum.stackexchange.com/questions/3373/how-to-clear-large-arrays-without-blowing-the-gas-limit)
* [政府彩票支付的拒绝服务燃气](https://www.reddit.com/r/ethereum/comments/4ghzhv/governmentals_1100_eth_jackpot_payout_is_stuck/)

## 示例

### dos_address.sol
```solidity
pragma solidity ^0.4.25;

contract DosGas {

    address[] creditorAddresses;
    bool win = false;

    function emptyCreditors() public {
        if(creditorAddresses.length>1500) {
            creditorAddresses = new address[](0);
            win = true;
        }
    }

    function addCreditors() public returns (bool) {
        for(uint i=0;i<350;i++) {
          creditorAddresses.push(msg.sender);
        }
        return true;
    }

    function iWin() public view returns (bool) {
        return win;
    }

    function numberCreditors() public view returns (uint) {
        return creditorAddresses.length;
    }
}
```

### dos_number.sol
```solidity
pragma solidity ^0.4.25;

contract DosNumber {

    uint numElements = 0;
    uint[] array;

    function insertNnumbers(uint value,uint numbers) public {

        // 如果数字大于382，那么Gas的DOS会有所增加或减少，具体取决于实际的Gas限制。
        for(uint i=0;i<numbers;i++) {
            if(numElements == array.length) {
                array.length += 1;
            }
            array[numElements++] = value;
        }
    }

    function clear() public {
        require(numElements>1500);
        numElements = 0;
    }

    // 气体DOS清除
    function clearDOS() public {

        // 数字取决于实际的燃气限制。
        require(numElements>1500);
        array = new uint[](0);
        numElements = 0;
    }

    function getLengthArray() public view returns(uint) {
        return numElements;
    }

    function getRealLengthArray() public view returns(uint) {
        return array.length;
    }
}
```

### dos_simple.sol

```solidity
pragma solidity ^0.4.25;

contract DosOneFunc {

    address[] listAddresses;

    function ifillArray() public returns (bool){
        if(listAddresses.length<1500) {

            for(uint i=0;i<350;i++) {
                listAddresses.push(msg.sender);
            }
            return true;

        } else {
            listAddresses = new address[](0);
            return false;
        }
    }
}
```