## 标题
右至左覆盖控制字符 (U+202E)

## 关联
[CWE-451: 用户界面 (UI) 误导关键信息](http://cwe.mitre.org/data/definitions/451.html)

## 描述
恶意行为者可以使用右至左覆盖的Unicode字符来强制进行RTL文本渲染，以混淆用户对合同真实意图的理解。

## 修复
U+202E字符有很少的合法用途，它不应该出现在智能合约的源代码中。

## 参考资料
[智能合约的智慧](https://youtu.be/P_Mtd5Fc_3E?t=1813)
## 样例
### guess_the_number.sol
```solidity

/*
 * @source: https://youtu.be/P_Mtd5Fc_3E
 * @author: Shahar Zini
 */
pragma solidity ^0.5.0;

contract GuessTheNumber
{
    uint _secretNumber;
    address payable _owner;
    event success(string);
    event wrongNumber(string);

    constructor(uint secretNumber) payable public
    {
        require(secretNumber <= 10);
        _secretNumber = secretNumber;
        _owner = msg.sender;
    }

    function getValue() view public returns (uint)
    {
        return address(this).balance;
    }

    function guess(uint n) payable public
    {
        require(msg.value == 1 ether);

        uint p = address(this).balance;
        checkAndTransferPrize(/*The prize‮/*rebmun desseug*/n , p/*‭
                /*应受益的用户 */,msg.sender);
    }

    function checkAndTransferPrize(uint p, uint n, address payable guesser) internal returns(bool)
    {
        if(n == _secretNumber)
        {
            guesser.transfer(p);
            emit success("You guessed the correct number!");
        }
        else
        {
            emit wrongNumber("You've made an incorrect guess!");
        }
    }

    function kill() public
    {
        require(msg.sender == _owner);
        selfdestruct(_owner);
    }
}
```