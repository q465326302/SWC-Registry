## 标题
右至左覆盖控制字符 (U+202E)

## 关联
[CWE-451: 用户界面 (UI) 误导关键信息](http://cwe.mitre.org/data/definitions/451.html)

## 描述
恶意行为者可能会利用Unicode字符的右至左覆盖功能，强制执行RTL文本渲染，从而混淆用户对智能合约真实意图的理解。

## 修复措施
U+202E字符在大多数情况下并无合法用途，因此，它不应出现在智能合约的源代码中。

## 参考资料
* [智能合约的智慧](https://youtu.be/P_Mtd5Fc_3E?t=1813)

## 示例

### guess_the_number.sol
```solidity
/*
 * @来源: https://youtu.be/P_Mtd5Fc_3E
 * @作者: Shahar Zini
 */
pragma solidity ^0.5.0;

contract GuessTheNumber
{
    uint _secretNumber;
    address payable _owner;
    event success(string);
    event wrongNumber(string);

    constructor(uint secretNumber) payable public
    {
        require(secretNumber <= 10);
        _secretNumber = secretNumber;
        _owner = msg.sender;
    }

    function getValue() view public returns (uint)
    {
        return address(this).balance;
    }

    function guess(uint n) payable public
    {
        require(msg.value == 1 ether);

        uint p = address(this).balance;
        checkAndTransferPrize(/*The prize‮/*rebmun desseug*/n , p/*‭
                /*应受益的用户 */,msg.sender);
    }

    function checkAndTransferPrize(uint p, uint n, address payable guesser) internal returns(bool)
    {
        if(n == _secretNumber)
        {
            guesser.transfer(p);
            emit success("You guessed the correct number!");
        }
        else
        {
            emit wrongNumber("You've made an incorrect guess!");
        }
    }

    function kill() public
    {
        require(msg.sender == _owner);
        selfdestruct(_owner);
    }
}
```

**评论**

这行代码

checkAndTransferPrize(/The prize‮/rebmun desseug/n , p/

在函数guess(uint n)中使用了不可见的方向控制字符，因此屏幕上显示的参数顺序与实际的顺序不符。函数的参数是n，p，地址，这是字符的逻辑顺序，但是有些字符从右到左显示，所以对读者来说，'n, p'这段看起来是反序的，这是因为代码中包含了不可见的方向控制字符。

通常，逐个选择文本字符会显示这一点——选择突然跳到从右到左的文本的末尾，并从右侧向左扩展。也可以在内容中明确检查unicode字符。