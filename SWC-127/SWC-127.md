## 标题
使用函数类型变量进行任意跳转

## 关系
[CWE-695: 使用低级功能](https://cwe.mitre.org/data/definitions/695.html)

## 描述
Solidity支持函数类型，这意味着可以将函数类型的变量赋值为与其签名匹配的函数引用。存储在这种变量中的函数可以像常规函数一样被调用。

然而，问题出现在用户可以随意更改函数类型变量并执行随机代码指令。虽然Solidity不支持指针算术运算，因此无法将此类变量更改为任意值，但如果开发者使用汇编指令（如mstore或赋值操作符），在最坏的情况下，攻击者可以将函数类型变量指向任何代码指令，从而违反所需的验证和状态更改。

## 修复措施
应尽量减少汇编的使用。开发者不应该允许用户将任意值赋给函数类型变量。

## 参考资料
* [Solidity CTF](https://medium.com/authio/solidity-ctf-part-2-safe-execution-ad6ded20e042)
* [Solidity文档- Solidity汇编](https://solidity.readthedocs.io/en/v0.4.25/assembly.html)
* [Solidity文档- 函数类型](https://solidity.readthedocs.io/en/v0.4.25/types.html#function-types)

## 示例

### FunctionTypes.sol
```solidity
/*
 * @来源: https://gist.github.com/wadeAlexC/7a18de852693b3f890560ab6a211a2b8
 * @作者: Alexander Wade
 */

pragma solidity ^0.4.25;

contract FunctionTypes {

    constructor() public payable { require(msg.value != 0); }

    function withdraw() private {
        require(msg.value == 0, 'dont send funds!');
        address(msg.sender).transfer(address(this).balance);
    }

    function frwd() internal
        { withdraw(); }

    struct Func { function () internal f; }

    function breakIt() public payable {
        require(msg.value != 0, 'send funds!');
        Func memory func;
        func.f = frwd;
        assembly { mstore(func, add(mload(func), callvalue)) }
        func.f();
    }
}
```