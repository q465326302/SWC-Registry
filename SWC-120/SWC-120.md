## 标题
链属性的弱随机性来源

## 关系
[CWE-330：使用不充分随机的值](https://cwe.mitre.org/data/definitions/330.html)

## 描述
在各种应用中，能够生成随机数非常有用。一个明显的例子是赌博DApps，其中伪随机数生成器用于选择赢家。然而，在以太坊中创建足够强大的随机性非常具有挑战性。例如，使用block.timestamp是不安全的，因为矿工可以选择在几秒钟内提供任何时间戳，仍然可以使他的区块被其他人接受。使用blockhash、block.difficulty和其他字段也是不安全的，因为它们由矿工控制。如果赌注很高，矿工可以通过租用硬件在短时间内挖掘大量区块，选择对他来说获胜所需的区块哈希，并丢弃其他所有区块。

## 修复措施
* 通过预言机使用外部随机性来源，并在链上进行密码学验证预言机的结果。例如，[Chainlink VRF](https://docs.chain.link/docs/chainlink-vrf)。这种方法不依赖于对预言机的信任，因为虚假生成的随机数将被系统的链上部分拒绝。
* 使用[承诺方案](https://en.wikipedia.org/wiki/Commitment_scheme)，例如[RANDAO](https://github.com/randao/randao)。
* 通过预言机使用外部随机性来源，例如[Oraclize](http://www.oraclize.it/)。请注意，这种方法需要对预言机的信任，因此可能合理地使用多个预言机。
* 使用比特币区块哈希，因为它们挖掘成本更高。

## 参考资料
* [如何在我的智能合约中安全生成随机数？](https://ethereum.stackexchange.com/questions/191/how-can-i-securely-generate-a-random-number-in-my-smart-contract)
* [BLOCKHASH何时可以安全地用于生成随机数？何时会不安全？](https://ethereum.stackexchange.com/questions/419/when-can-blockhash-be-safely-used-for-a-random-number-when-would-it-be-unsafe)
* [Run智能合约](https://etherscan.io/address/0xcac337492149bdb66b088bf5914bedfbf78ccc18)

## 示例

### guess_the_random_number.sol
```solidity

/*
 * @来源: https://capturetheether.com/challenges/lotteries/guess-the-random-number/
 * @作者: Steve Marx
 */

pragma solidity ^0.4.21;

contract GuessTheRandomNumberChallenge {
    uint8 answer;

    function GuessTheRandomNumberChallenge() public payable {
        require(msg.value == 1 ether);
        answer = uint8(keccak256(block.blockhash(block.number - 1), now));
    }

    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function guess(uint8 n) public payable {
        require(msg.value == 1 ether);

        if (n == answer) {
            msg.sender.transfer(2 ether);
        }
    }
}
```

### guess_the_random_number_fixed.sol
```solidity
/*
 * @来源: https://capturetheether.com/challenges/lotteries/guess-the-random-number/
 * @作者: Steve Marx
 */

pragma solidity ^0.4.25;

contract GuessTheRandomNumberChallenge {
    uint8 answer;
    uint8 commitedGuess;
    uint commitBlock;
    address guesser;

    function GuessTheRandomNumberChallenge() public payable {
        require(msg.value == 1 ether);
    }

    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    //猜测你的区块哈希之后20个区块的模数。
    function guess(uint8 _guess) public payable {
        require(msg.value == 1 ether);
        commitedGuess = _guess;
        commitBlock = block.number;
        guesser = msg.sender;
    }
    function recover() public {
      //在猜测的区块之后、commitBlock+20的区块哈希不可恢复之前，必须调用此函数。
      require(block.number > commitBlock + 20 && commitBlock+20 > block.number - 256);
      require(guesser == msg.sender);

      if(uint(blockhash(commitBlock+20)) == commitedGuess){
        msg.sender.transfer(2 ether);
      }
    }
}
```

### old_blockhash.sol
```solidity
pragma solidity ^0.4.24;

//根据https://capturetheether.com/challenges/lotteries/predict-the-block-hash/上的Capture the Ether挑战

//请注意，尽管你似乎有1/2^256的机会猜对哈希值，实际上，对于超过256个区块之前的区块号，blockhash函数会返回零，所以你可以猜测零并等待。
contract PredictTheBlockHashChallenge {

    struct guess{
      uint block;
      bytes32 guess;
    }

    mapping(address => guess) guesses;

    constructor() public payable {
        require(msg.value == 1 ether);
    }

    function lockInGuess(bytes32 hash) public payable {
        require(guesses[msg.sender].block == 0);
        require(msg.value == 1 ether);

        guesses[msg.sender].guess = hash;
        guesses[msg.sender].block  = block.number + 1;
    }

    function settle() public {
        require(block.number > guesses[msg.sender].block);

        bytes32 answer = blockhash(guesses[msg.sender].block);

        guesses[msg.sender].block = 0;
        if (guesses[msg.sender].guess == answer) {
            msg.sender.transfer(2 ether);
        }
    }
}
```

### old_blockhash_fixed.sol
```solidity
pragma solidity ^0.4.24;

//根据https://capturetheether.com/challenges/lotteries/predict-the-block-hash/上的Capture the Ether挑战

//请注意，尽管你似乎有1/2^256的机会猜对哈希值，实际上，对于超过256个区块之前的区块号，blockhash函数会返回零，所以你可以猜测零并等待。
contract PredictTheBlockHashChallenge {

    struct guess{
      uint block;
      bytes32 guess;
    }

    mapping(address => guess) guesses;

    constructor() public payable {
        require(msg.value == 1 ether);
    }

    function lockInGuess(bytes32 hash) public payable {
        require(guesses[msg.sender].block == 0);
        require(msg.value == 1 ether);

        guesses[msg.sender].guess = hash;
        guesses[msg.sender].block  = block.number + 1;
    }

    function settle() public {
        require(block.number > guesses[msg.sender].block +10);
        //请注意，这个解决方案可以防止 blockhash(guesses[msg.sender].block) 为零的攻击。
        //此外，我们还添加了十个区块的冷却期，以防止矿工利用对下一个区块哈希的预知。
        if(guesses[msg.sender].block - block.number < 256){
          bytes32 answer = blockhash(guesses[msg.sender].block);

          guesses[msg.sender].block = 0;
          if (guesses[msg.sender].guess == answer) {
              msg.sender.transfer(2 ether);
          }
        }
        else{
          revert("Sorry your lottery ticket has expired");
        }
    }
}
```

### random_number_generator.sol

```solidity
pragma solidity ^0.4.25;

// 基于部署在0xcac337492149bDB66b088bf5914beDfBf78cCC18的TheRun合约。
contract RandomNumberGenerator {
  uint256 private salt =  block.timestamp;

  function random(uint max) view private returns (uint256 result) {
    // 获取最佳的随机种子
    uint256 x = salt * 100 / max;
    uint256 y = salt * block.number / (salt % 5);
    uint256 seed = block.number / 3 + (salt % 300) + y;
    uint256 h = uint256(blockhash(seed));
    // 在1和最大值之间生成随机数
    return uint256((h / x)) % max + 1;
  }
}

```