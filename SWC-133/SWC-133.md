## 标题  未完成
多变长度参数的哈希冲突

## 关联
[CWE-294：通过捕获重放绕过身份验证](https://cwe.mitre.org/data/definitions/294.html)

## 描述
在某些情况下，使用abi.encodePacked()与多个变长参数可能会导致哈希冲突。由于abi.encodePacked()按顺序打包所有元素，无论它们是否是数组的一部分，你可以在数组之间移动元素，并且只要所有元素的顺序相同，它将返回相同的编码。在签名验证情况下，攻击者可以通过修改先前函数调用中的元素位置来绕过授权。

## 纠正措施
在使用abi.encodePacked()时，关键是确保不能使用不同的参数实现匹配的签名。为此，要么不允许用户访问abi.encodePacked()中使用的参数，要么使用固定长度的数组。另外，你也可以简单地使用abi.encode()。
还建议使用重放保护（参见[SWC-121]），尽管攻击者仍然可以通过[前置交易]来绕过此保护。

## 参考资料
[Solidity非标准打包模式](https://solidity.readthedocs.io/en/v0.5.3/abi-spec.html#non-standard-packed-mode)
[哈希冲突攻击](https://learncryptography.com/hash-functions/hash-collision-attack)
## 示例
### access_control.sol
```solidity

/*
 * @author: Steve Marx
 */

pragma solidity ^0.5.0;

import "./ECDSA.sol";

contract AccessControl {
    using ECDSA for bytes32;
    mapping(address => bool) isAdmin;
    mapping(address => bool) isRegularUser;
    // 添加管理员和普通用户。
    function addUsers(
        address[] calldata admins,
        address[] calldata regularUsers,
        bytes calldata signature
    )
        external
    {
        if (!isAdmin[msg.sender]) {
            // 允许通过管理员的签名中继calls。
            bytes32 hash = keccak256(abi.encodePacked(admins, regularUsers));
            address signer = hash.toEthSignedMessageHash().recover(signature);
            require(isAdmin[signer], "Only admins can add users.");
        }
        for (uint256 i = 0; i < admins.length; i++) {
            isAdmin[admins[i]] = true;
        }
        for (uint256 i = 0; i < regularUsers.length; i++) {
            isRegularUser[regularUsers[i]] = true;
        }
    }
}
```
### access_control_fixed_1.sol
```solidity
/*
 * @author: Steve Marx
 * Modified by Kaden Zipfel
 */

pragma solidity ^0.5.0;

import "./ECDSA.sol";

contract AccessControl {
    using ECDSA for bytes32;
    mapping(address => bool) isAdmin;
    mapping(address => bool) isRegularUser;
    // 添加一个单一用户，可以是管理员或普通用户。
    function addUser(
        address user,
        bool admin,
        bytes calldata signature
    )
        external
    {
        if (!isAdmin[msg.sender]) {
            // 允许使用管理员的签名转发calls。
            bytes32 hash = keccak256(abi.encodePacked(user));
            address signer = hash.toEthSignedMessageHash().recover(signature);
            require(isAdmin[signer], "Only admins can add users.");
        }
        if (admin) {
            isAdmin[user] = true;
        } else {
            isRegularUser[user] = true;
        }
    }
}
```

### access_control_fixed_2.sol

```solidity
/*
 * @author: Steve Marx
 * Modified by Kaden Zipfel
 */

pragma solidity ^0.5.0;

import "./ECDSA.sol";

contract AccessControl {
    using ECDSA for bytes32;
    mapping(address => bool) isAdmin;
    mapping(address => bool) isRegularUser;
    // 添加管理员和普通用户。
    function addUsers(
        // 使用固定长度的数组。
        address[3] calldata admins,
        address[3] calldata regularUsers,
        bytes calldata signature
    )
        external
    {
        if (!isAdmin[msg.sender]) {
            // 允许使用管理员的签名转发calls。
            bytes32 hash = keccak256(abi.encodePacked(admins, regularUsers));
            address signer = hash.toEthSignedMessageHash().recover(signature);
            require(isAdmin[signer], "Only admins can add users.");
        }
        for (uint256 i = 0; i < admins.length; i++) {
            isAdmin[admins[i]] = true;
        }
        for (uint256 i = 0; i < regularUsers.length; i++) {
            isRegularUser[regularUsers[i]] = true;
        }
    }
}

```