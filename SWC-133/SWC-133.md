## 标题 
多个可变长度参数的哈希碰撞

## 关联
[CWE-294：通过捕获重放绕过身份验证](https://cwe.mitre.org/data/definitions/294.html)

## 描述
在某些情况下，使用abi.encodePacked()和多个可变长度参数可能会导致哈希冲突。这是因为abi.encodePacked()会按顺序打包所有元素，无论这些元素是否属于数组。因此，只要所有元素的顺序保持不变，即使在数组之间移动元素，它也会返回相同的编码。在签名验证的场景中，攻击者可能会利用这个特性，通过在之前的函数调用中改变元素的位置，实际上绕过了授权。

## 修复措施
在使用abi.encodePacked()时，确保不能使用不同的参数来生成相同的签名。为了做到这一点，要么不允许用户访问abi.encodePacked()中使用的参数，要么使用固定长度的数组。你也可以选择使用abi.encode()来替代abi.encodePacked()。
还建议使用重放保护（参见[SWC-121](SWC-121/SWC-121.md)），尽管攻击者仍然可以通过[前置交易](SWC-114/SWC-114.md)来绕过此保护。

## 参考资料
* [Solidity非标准打包模式](https://solidity.readthedocs.io/en/v0.5.3/abi-spec.html#non-standard-packed-mode)
* [哈希冲突攻击](https://learncryptography.com/hash-functions/hash-collision-attack)

## 示例

### access_control.sol
```solidity

/*
 * @作者: Steve Marx
 */

pragma solidity ^0.5.0;

import "./ECDSA.sol";

contract AccessControl {
    using ECDSA for bytes32;
    mapping(address => bool) isAdmin;
    mapping(address => bool) isRegularUser;
    // 添加管理员和普通用户。
    function addUsers(
        address[] calldata admins,
        address[] calldata regularUsers,
        bytes calldata signature
    )
        external
    {
        if (!isAdmin[msg.sender]) {
            // 允许通过管理员的签名中继calls。
            bytes32 hash = keccak256(abi.encodePacked(admins, regularUsers));
            address signer = hash.toEthSignedMessageHash().recover(signature);
            require(isAdmin[signer], "Only admins can add users.");
        }
        for (uint256 i = 0; i < admins.length; i++) {
            isAdmin[admins[i]] = true;
        }
        for (uint256 i = 0; i < regularUsers.length; i++) {
            isRegularUser[regularUsers[i]] = true;
        }
    }
}
```

### access_control_fixed_1.sol
```solidity
/*
 * @作者: Steve Marx
 * 由Kaden Zipfel修改
 */

pragma solidity ^0.5.0;

import "./ECDSA.sol";

contract AccessControl {
    using ECDSA for bytes32;
    mapping(address => bool) isAdmin;
    mapping(address => bool) isRegularUser;
    // 添加一个单一用户，可以是管理员或普通用户。
    function addUser(
        address user,
        bool admin,
        bytes calldata signature
    )
        external
    {
        if (!isAdmin[msg.sender]) {
            // 允许使用管理员的签名转发calls。
            bytes32 hash = keccak256(abi.encodePacked(user));
            address signer = hash.toEthSignedMessageHash().recover(signature);
            require(isAdmin[signer], "Only admins can add users.");
        }
        if (admin) {
            isAdmin[user] = true;
        } else {
            isRegularUser[user] = true;
        }
    }
}
```

### access_control_fixed_2.sol
```solidity
/*
 * @作者: Steve Marx
 * 由Kaden Zipfel修改
 */

pragma solidity ^0.5.0;

import "./ECDSA.sol";

contract AccessControl {
    using ECDSA for bytes32;
    mapping(address => bool) isAdmin;
    mapping(address => bool) isRegularUser;
    // 添加管理员和普通用户。
    function addUsers(
        // 使用固定长度的数组。
        address[3] calldata admins,
        address[3] calldata regularUsers,
        bytes calldata signature
    )
        external
    {
        if (!isAdmin[msg.sender]) {
            // 允许使用管理员的签名转发calls。
            bytes32 hash = keccak256(abi.encodePacked(admins, regularUsers));
            address signer = hash.toEthSignedMessageHash().recover(signature);
            require(isAdmin[signer], "Only admins can add users.");
        }
        for (uint256 i = 0; i < admins.length; i++) {
            isAdmin[admins[i]] = true;
        }
        for (uint256 i = 0; i < regularUsers.length; i++) {
            isRegularUser[regularUsers[i]] = true;
        }
    }
}

```