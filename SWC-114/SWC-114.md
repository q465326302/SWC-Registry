## 标题
交易顺序依赖

## 关系
[CWE-362：使用不正确的同步机制进行并发执行（'竞争条件'）](https://cwe.mitre.org/data/definitions/362.html)

## 描述
以太坊网络以区块的形式处理交易，每隔约17秒确认一个新的区块。矿工会审核他们收到的交易，并根据支付了足够高的燃气费的交易来决定将哪些交易包含在区块中。此外，当交易发送到以太坊网络时，它们会被转发到每个节点进行处理。因此，运行以太坊节点的人可以在交易最终确认之前预测哪些交易将发生。当代码依赖于提交给它的交易的顺序时，就会出现竞争条件漏洞。

竞争条件最简单的例子是智能合约为提交信息提供奖励。假设一个合约会向首个解决数学问题的人发放1个代币作为奖励。Alice解决了这个问题，并以标准燃气费用将答案提交到网络中。Eve运行一个以太坊节点，可以在Alice提交到网络的交易中看到这个数学问题的答案。因此，Eve以更高的燃气费用将答案提交到网络，并因此在Alice的交易之前得到处理和确认。Eve获得了1个代币，而Alice却什么都没有得到，尽管是Alice努力解决问题的。在实际中，这种情况经常发生在合约奖励人们揭示协议中的不良行为，通过将不良行为者的存款给证明自己的人。

目前网络上最常见的竞争条件是ERC20代币标准中的竞争条件。ERC20代币标准包括一个名为“approve”的函数，允许一个地址批准另一个地址代表自己花费代币。假设Alice已经批准Eve花费她的n个代币，然后Alice决定将Eve的批准更改为m个代币。Alice向approve提交一个值为n的函数调用。Eve运行一个以太坊节点，所以她知道Alice将要将她的批准更改为m。然后Eve提交一个transferFrom请求，将Alice的n个代币转移到自己的地址，并为此给出了比Alice的交易高得多的燃气费用。transferFrom先执行，给了Eve n个代币，并将Eve的批准设置为零。然后Alice的交易执行，将Eve的批准设置为m。然后Eve也将这m个代币发送给了自己。因此，尽管Eve最多只能获得n和m个代币之和，她却获得了n + m个代币。

## 修复措施
解决提交信息以换取奖励的竞争条件的一种可能方法是使用提交和揭示哈希方案。提交者不再提交答案，而是提交哈希值（盐、地址、答案）[盐是他们选择的一些数字]；合约存储这个哈希值和发送者的地址。为了领取奖励，发送者随后提交一个带有盐和答案的交易。合约对（盐、msg.sender、答案）进行哈希，并将产生的哈希与存储的哈希进行比较。如果哈希匹配，合约释放奖励。

修复ERC20竞争条件的最佳方法是在approve的输入中添加一个字段，即预期的当前值，并在Eve的当前允许额不符合Alice指示的预期值时使approve回滚。但是，这意味着您的合约不再符合ERC20标准。如果对于您的项目而言重要的是使合约符合ERC20，可以添加一个安全的approve函数。从用户的角度来看，可以通过在更改之前将批准设置为零来减轻ERC20竞争条件。

## 参考资料
[竞争条件的一般文章](https://medium.com/coinmonks/solidity-transaction-ordering-attacks-1193a014884e) [ERC20竞争条件](https://docs.google.com/document/d/1YLPtQxZu1UAvO9cZ1O2RPXBbT0mooh4DYKjA_jp-RLM/edit#)

## 示例
### ERC20.sol
```solidity

pragma solidity ^0.4.24;

/** Taken from the OpenZeppelin github
 * @title SafeMath
 * @使用安全检查的开发数学操作，出错时进行回滚。
 */
library SafeMath {

  /**
  * @开发者编写了一个函数，用于计算两个数字的乘积，当结果溢出时会进行反转。
  */
  function mul(uint256 a, uint256 b) internal pure returns (uint256) {
    // 气体优化：这比要求 'a' 不为零要便宜，但如果还要测试 'b'，则会失去这个好处。
    // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522
    if (a == 0) {
      return 0;
    }

    uint256 c = a * b;
    require(c / a == b);

    return c;
  }

  /**
  * @将两个数字进行整数除法，截断商，如果除数为零则返回。
  */
  function div(uint256 a, uint256 b) internal pure returns (uint256) {
    require(b > 0); // Solidity only automatically asserts when dividing by 0
    uint256 c = a / b;
    // 断言（assert）(a == b * c + a % b); // 在任何情况下都不会违反这个条件

    return c;
  }

  /**
  * @开发者将两个数字相减，如果被减数大于减数，则返回溢出。
  */
  function sub(uint256 a, uint256 b) internal pure returns (uint256) {
    require(b <= a);
    uint256 c = a - b;

    return c;
  }

  /**
  * @开发者将两个数字相加，在溢出时进行还原。
  */
  function add(uint256 a, uint256 b) internal pure returns (uint256) {
    uint256 c = a + b;
    require(c >= a);

    return c;
  }

  /**
  * @dev函数用于两个数相除并返回余数（无符号整数模除），当除以零时返回错误。
  */
  function mod(uint256 a, uint256 b) internal pure returns (uint256) {
    require(b != 0);
    return a % b;
  }
}


contract ERC20 {

  event Transfer( address indexed from, address indexed to, uint256 value );
  event Approval( address indexed owner, address indexed spender, uint256 value);
  using SafeMath for *;

  mapping (address => uint256) private _balances;

  mapping (address => mapping (address => uint256)) private _allowed;

  uint256 private _totalSupply;

  constructor(uint totalSupply){
    _balances[msg.sender] = totalSupply;
  }

  function balanceOf(address owner) public view returns (uint256) {
    return _balances[owner];
  }


  function allowance(address owner, address spender) public view returns (uint256)
  {
    return _allowed[owner][spender];
  }

  function transfer(address to, uint256 value) public returns (bool) {
    require(value <= _balances[msg.sender]);
    require(to != address(0));

    _balances[msg.sender] = _balances[msg.sender].sub(value);
    _balances[to] = _balances[to].add(value);
    emit Transfer(msg.sender, to, value);
    return true;
  }

  function approve(address spender, uint256 value) public returns (bool) {
    require(spender != address(0));

    _allowed[msg.sender][spender] = value;
    emit Approval(msg.sender, spender, value);
    return true;
  }

  function transferFrom(address from, address to, uint256 value) public returns (bool) {
    require(value <= _balances[from]);
    require(value <= _allowed[from][msg.sender]);
    require(to != address(0));

    _balances[from] = _balances[from].sub(value);
    _balances[to] = _balances[to].add(value);
    _allowed[from][msg.sender] = _allowed[from][msg.sender].sub(value);
    emit Transfer(from, to, value);
    return true;
  }
}
```

### eth_tx_order_dependence_minimal.sol
```solidity
/*
 * @source: https://github.com/ConsenSys/evm-analyzer-benchmark-suite
 * @author: Suhabe Bugrara
 */

pragma solidity ^0.4.16;

contract EthTxOrderDependenceMinimal {
    address public owner;
    bool public claimed;
    uint public reward;

    function EthTxOrderDependenceMinimal() public {
        owner = msg.sender;
    }

    function setReward() public payable {
        require (!claimed);

        require(msg.sender == owner);
        owner.transfer(reward);
        reward = msg.value;
    }

    function claimReward(uint256 submission) {
        require (!claimed);
        require(submission < 10);

        msg.sender.transfer(reward);
        claimed = true;
    }
}
```