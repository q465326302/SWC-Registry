# SWC-135

## 标题
没有效果的代码

## 关系
CWE-1164：无关代码

## 描述
在Solidity中，可以编写没有产生预期效果的代码。目前，Solidity编译器不会对无效果的代码返回警告。这可能导致引入不正确执行预期操作的“死”代码。

例如，很容易忽略msg.sender.call.value(address(this).balance)("")中的括号，这可能导致函数在不向msg.sender转移资金的情况下继续进行。虽然通过检查调用的返回值可以避免这种情况。

## 纠正措施
确保您的合约按预期工作非常重要。编写单元测试来验证代码的正确行为。

## 参考资料
[Solidity的Github问题 - 当语句永远不会产生副作用时引发错误](https://github.com/ethereum/solidity/issues/2707)
[Solidity的Github问题 - msg.sender.call.value(address(this).balance);应该产生警告](https://github.com/ethereum/solidity/issues/7096)
## 合约示例

### deposit_box.sol
```solidity
pragma solidity ^0.5.0;

contract DepositBox {
    mapping(address => uint) balance;

    // 接受存款
    function deposit(uint amount) public payable {
        require(msg.value == amount, 'incorrect amount');
        // 应更新用户余额
        balance[msg.sender] == amount;
    }
}
```

### deposit_box.yaml
```yaml
description: 无影响声明的存款箱
issues:
- id: SWC-135
  count: 1
  locations: 
  - bytecode_offsets: {}
    line_numbers:
      deposit_box.sol: [10]
```

### deposit_box_fixed.sol
```solidity
pragma solidity ^0.5.0;

contract DepositBox {
    mapping(address => uint) balance;

    // 接受存款
    function deposit(uint amount) public payable {
        require(msg.value == amount, 'incorrect amount');
        // 应更新用户余额
        balance[msg.sender] = amount;
    }
}
```

### deposit_box_fixed.yaml
```yaml
description: 无影响声明的存款箱 (fixed)
issues:
- id: SWC-135
  count: 0
  locations: []
```

### wallet.sol
```solidity
/*
 * @author: Kaden Zipfel
 */

pragma solidity ^0.5.0;

contract Wallet {
    mapping(address => uint) balance;

    // 将资金存入合约
    function deposit(uint amount) public payable {
        require(msg.value == amount, 'msg.value must be equal to amount');
        balance[msg.sender] = amount;
    }

    // 从合同中提取资金
    function withdraw(uint amount) public {
        require(amount <= balance[msg.sender], 'amount must be less than balance');

        uint previousBalance = balance[msg.sender];
        balance[msg.sender] = previousBalance - amount;

        // 尝试将金额从合约发送给消息发送者
        msg.sender.call.value(amount);
    }
}
```
### wallet.yaml
```yaml
description: 钱包合约具有无副作用语句。（修正版）
issues:
- id: SWC-135
  count: 1
  locations: 
  - bytecode_offsets: {}
    line_numbers:
      wallet.sol: [24]

```

### wallet_fixed.sol
```solidity
/*
 * @author: Kaden Zipfel
 */

pragma solidity ^0.5.0;

contract Wallet {
    mapping(address => uint) balance;

    // 在合约中存入资金
    function deposit(uint amount) public payable {
        require(msg.value == amount, 'msg.value must be equal to amount');
        balance[msg.sender] = amount;
    }

    // 从合约中提取资金
    function withdraw(uint amount) public {
        require(amount <= balance[msg.sender], 'amount must be less than balance');

        uint previousBalance = balance[msg.sender];
        balance[msg.sender] = previousBalance - amount;

        // 尝试将金额从合约发送给消息发送者
        (bool success, ) = msg.sender.call.value(amount)("");
        require(success, 'transfer failed');
    }
}
```
### wallet_fixed.yaml

```yaml
description: 带有无副作用语句的钱包合约（修正版）
issues:
- id: SWC-135
  count: 0
  locations: []

```