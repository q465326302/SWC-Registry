## 标题
存在未使用的变量

## 关系
[CWE-1164: 无关代码](https://cwe.mitre.org/data/definitions/1164.html)

## 描述
在Solidity中允许存在未使用的变量，并且它们并不会直接造成安全问题。但最好的做法是避免使用它们，因为它们可能会：

* 导致计算量增加（以及不必要的燃气消耗）
* 表明存在错误或格式错误的数据结构，通常是代码质量差的一个标志
* 导致代码冗余，降低代码的可读性
## 修复措施
从代码库中删除所有未使用的变量。

## 参考资料
* [未使用的局部变量警告讨论](https://github.com/ethereum/solidity/issues/718)
* [继承状态变量的屏蔽讨论](https://github.com/ethereum/solidity/issues/2563)

## 示例

### unused_state_variables.sol
```solidity
pragma solidity >=0.5.0;
pragma experimental ABIEncoderV2;

import "./base.sol";

contract DerivedA is Base {
    // 当前合约中没有使用i
    A i = A(1);

    int internal j = 500;

    function call(int a) public {
        assign1(a);
    }

    function assign3(A memory x) public returns (uint) {
        return g[1] + x.a + uint(j);
    }

    function ret() public returns (int){
        return this.e();

    }
}
```

### unused_state_variables_fixed.sol
```solidity
pragma solidity >=0.5.0;
pragma experimental ABIEncoderV2;

import "./base_fixed.sol";

contract DerivedA is Base {

    int internal j = 500;

    function call(int a) public {
        assign1(a);
    }

    function assign3(A memory x) public returns (uint) {
        return g[1] + x.a + uint(j);
    }

    function ret() public returns (int){
        return this.e();

    }

}
```

### unused_variables.sol
```solidity
pragma solidity ^0.5.0;

contract UnusedVariables {
    int a = 1;

    // y未被使用
    function unusedArg(int x, int y) public view returns (int z) {
        z = x + a;  
    }

    // n没有被报告，它是另一个SWC类别的一部分。
    function unusedReturn(int x, int y) public pure returns (int m, int n, int o) {
        m = y - x;
        o = m/2;
    }

    // x没有被访问
    function neverAccessed(int test) public pure returns (int) {
        int z = 10;

        if (test > z) {
            // x 没有被使用
            int x = test - z;

            return test - z;
        }

        return z;
    }

    function tupleAssignment(int p) public returns (int q, int r){
        (q, , r) = unusedReturn(p,2);

    }


}
```

### unused_variables_fixed.sol
```solidity
pragma solidity ^0.5.0;

contract UnusedVariables {
    int a = 1;

    function unusedArg(int x) public view returns (int z) {
        z = x + a;  
    }

    // n没有被报告，它是另一个SWC类别的一部分。
    function unusedReturn(int x, int y) public pure returns (int m, int n,int o) {
        m = y - x;
        o = m/2;
    }

    // x没有被访问
    function neverAccessed(int test) public pure returns (int) {
        int z = 10;

        if (test > z) {
            return test - z;
        }

        return z;
    }

    function tupleAssignment(int p) public returns (int q, int r){
        (q, , r) = unusedReturn(p,2);

    }

}
```