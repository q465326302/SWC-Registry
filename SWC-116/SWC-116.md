## 标题
使用区块值作为时间的代理

## 关系
[CWE-829：在不受信任的控制领域中包含功能](https://cwe.mitre.org/data/definitions/829.html)

## 描述
合约通常需要访问时间值以执行特定类型的功能。例如，block.timestamp和block.number可以让你了解当前时间或时间差，但是它们在大多数情况下并不安全。
在block.timestamp的情况下，开发人员经常尝试使用它来触发时间相关的事件。由于以太坊是去中心化的，节点只能在一定程度上同步时间。此外，恶意的矿工可以更改自己区块的时间戳，特别是如果他们这样做可以获得利益。然而，矿工不能将时间戳设置为小于上一个时间戳（否则该区块将被拒绝），也不能将时间戳设置得太远。考虑到上述所有情况，开发人员不能依赖所提供时间戳的准确性。
至于block.number，考虑到以太坊的区块时间通常约为14秒，可以预测区块之间的时间差。然而，区块时间并不是恒定的，并且受多种原因的影响而可能发生变化，例如分叉重组和难度炸弹。由于区块时间的变化，也不应该依赖block.number来进行精确的时间计算。

## 修复措施
开发人员应该意识到区块值不是精确的，并且使用它们可能会导致意想不到的影响。作为替代方案，他们可以利用预言机。

## 参考资料
* [安全性：依赖时间戳](https://github.com/ethereum/wiki/wiki/Safety#timestamp-dependence)
* [以太坊智能合约最佳实践-依赖时间戳](https://consensys.github.io/smart-contract-best-practices/development-recommendations/solidity-specific/timestamp-dependence/)
* [以太坊挖矿节点如何与网络保持时间一致？](https://ethereum.stackexchange.com/questions/5924/how-do-ethereum-mining-nodes-maintain-a-time-consistent-with-the-network/5926#5926)
* [Solidity：时间戳依赖，是否可以安全地使用？](https://ethereum.stackexchange.com/questions/15047/solidity-timestamp-dependency-is-it-possible-to-do-safely)
* [避免使用block.number作为时间戳](https://consensys.github.io/smart-contract-best-practices/development-recommendations/solidity-specific/timestamp-dependence/#avoid-using-blocknumber-as-a-timestamp)

## 示例

### time_lock.sol
```solidity

/*
 * @作者: Kaden Zipfel
 */

pragma solidity ^0.5.0;

contract TimeLock {
    struct User {
        uint amount; // 锁定金额（以以太计）
        uint unlockBlock; // 解锁以太坊的最小区块
    }

    mapping(address => User) private users;

    // 令牌应该在指定的确切时间内被锁定
    function lockEth(uint _time, uint _amount) public payable {
        require(msg.value == _amount, 'must send exact amount');
        users[msg.sender].unlockBlock = block.number + (_time / 14);
        users[msg.sender].amount = _amount;
    }

    // 如果锁定期已过，可以提取代币。
    function withdraw() public {
        require(users[msg.sender].amount > 0, 'no amount locked');
        require(block.number >= users[msg.sender].unlockBlock, 'lock period not over');

        uint amount = users[msg.sender].amount;
        users[msg.sender].amount = 0;
        (bool success, ) = msg.sender.call.value(amount)("");
        require(success, 'transfer failed');
    }
}
```

### timed_crowdsale.sol
```solidity
pragma solidity ^0.5.0;

contract TimedCrowdsale {

  event Finished();
  event notFinished();

  // 销售应该在2019年1月1日准确结束。
  function isSaleFinished() private returns (bool) {
    return block.timestamp >= 1546300800;
  }

  function run() public {
    if (isSaleFinished()) {
        emit Finished();
    } else {
        emit notFinished();
    }
  }

}

```