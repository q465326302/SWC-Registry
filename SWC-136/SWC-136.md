# SWC-136
## 标题
未加密的私人数据上链
## 关系
[CWE-767：通过公共方法访问关键私有变量](https://cwe.mitre.org/data/definitions/767.html)
## 描述
人们普遍误解私有类型变量无法被读取。即使你的合约未发布，攻击者也可以查看合约交易以确定存储在合约状态中的值。因此，重要的是不要将未加密的私人数据存储在合约代码或状态中。

## 修复措施
任何私人数据应该存储在链下，或者进行仔细加密。

## 参考资料
* [在以太坊上保守秘密](https://medium.com/solidified/keeping-secrets-on-ethereum-5b556c3bb1ee)
* [对以太坊智能合约攻击的调查（SoK）](https://www.semanticscholar.org/paper/A-Survey-of-Attacks-on-Ethereum-Smart-Contracts-Atzei-Bartoletti/aec843c0f38aff6c7901391a75ec10114a3d60f8)
* [未加密的秘密](https://github.com/KadenZipfel/smart-contract-attack-vectors/blob/master/vulnerabilities/unencrypted-secrets.md)
* [Stack Overflow - 在链上解密消息](https://ethereum.stackexchange.com/questions/69825/decrypt-message-on-chain)

## 示例

### odd_even.sol
```solidity
/*
 * @来源: https://gist.github.com/manojpramesh/336882804402bee8d6b99bea453caadd#file-odd-even-sol
 * @作者: https://github.com/manojpramesh
 * 由Kaden Zipfel修改
 */

pragma solidity ^0.5.0;

contract OddEven {
    struct Player {
        address addr;
        uint number;
    }

    Player[2] private players;
    uint count = 0;

    function play(uint number) public payable {
            require(msg.value == 1 ether, 'msg.value must be 1 eth');
            players[count] = Player(msg.sender, number);
            count++;
            if (count == 2) selectWinner();
    }

    function selectWinner() private {
            uint n = players[0].number + players[1].number;
            (bool success, ) = players[n%2].addr.call.value(address(this).balance)("");
            require(success, 'transfer failed');
            delete players;
            count = 0;
    }
}
```

### odd_even_fixed.sol
```solidity
/*
 * @来源: https://github.com/yahgwai/rps
 * @作者: Chris Buckland
 * 由Kaden Zipfel修改
 */

pragma solidity ^0.5.0;

contract OddEven {
    enum Stage {
        FirstCommit,
        SecondCommit,
        FirstReveal,
        SecondReveal,
        Distribution
    }

    struct Player {
        address addr;
        bytes32 commitment;
        uint number;
    }

    Player[2] private players;
    Stage public stage = Stage.FirstCommit;

    function play(bytes32 commitment) public payable {
        // 仅在提交阶段运行
        uint playerIndex;
        if(stage == Stage.FirstCommit) playerIndex = 0;
        else if(stage == Stage.SecondCommit) playerIndex = 1;
        else revert("only two players allowed");

        // 需要适当的存款金额
        // 将1个以太作为赌注 + 1个以太作为保证金
        require(msg.value == 2 ether, 'msg.value must be 2 eth');

        // 存储承诺
        players[playerIndex] = Player(msg.sender, commitment, 0);

        // 进入下一个阶段
        if(stage == Stage.FirstCommit) stage = Stage.SecondCommit;
        else stage = Stage.FirstReveal;
    }

    function reveal(uint number, bytes32 blindingFactor) public {
        // 仅在揭示阶段运行
        require(stage == Stage.FirstReveal || stage == Stage.SecondReveal, "wrong stage");

        // 找到玩家索引
        uint playerIndex;
        if(players[0].addr == msg.sender) playerIndex = 0;
        else if(players[1].addr == msg.sender) playerIndex = 1;
        else revert("unknown player");

        // 检查哈希值以证明玩家的诚实性
        require(keccak256(abi.encodePacked(msg.sender, number, blindingFactor)) == players[playerIndex].commitment, "invalid hash");

        // 如果正确，请更新玩家数量
        players[playerIndex].number = number;

        // 进入下一个阶段
        if(stage == Stage.FirstReveal) stage = Stage.SecondReveal;
        else stage = Stage.Distribution;
    }

    function distribute() public {
        // 只在分发阶段运行
        require(stage == Stage.Distribution, "wrong stage");

        // 找到获胜者
        uint n = players[0].number + players[1].number;

        // 支付获胜者的奖金和债券
        players[n%2].addr.call.value(3 ether)("");

        // 回报失败者的债券
        players[(n+1)%2].addr.call.value(1 ether)("");

        // 重置状态
        delete players;
        stage = Stage.FirstCommit;
    }
}
```